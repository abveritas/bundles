
pkgname=libunique
pkgver=1.1.6
pkgrel=3
pkgdesc="Library for writing single instance applications"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gtk2' 'dbus-glib')
makedepends=('pkgconfig' 'gobject-introspection')
options=('!libtool')
url="http://live.gnome.org/LibUnique"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/1.1/${pkgname}-${pkgver}.tar.bz2
        unique-gdbus.patch
        remove_G_CONST_RETURN.patch)
sha256sums=('e5c8041cef8e33c55732f06a292381cb345db946cf792a4ae18aa5c66cdd4fbb'
            'bf99ddbf9104502ea498a6812ec2d700f61f4dbbaa2471cd52174b0b27070769'
            '026da9583644217bc935a8f3e547ff0489cde2af046e336d306613c9ce3c1be0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/unique-gdbus.patch"
  patch -p1 -i "${srcdir}/remove_G_CONST_RETURN.patch"
  autoreconf
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --disable-dbus
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Remove documentation, provided by libunique3
  rm -r "$pkgdir/usr/share/gtk-doc"
}


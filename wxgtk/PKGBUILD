#
# Packages for Chakra, part of chakra-project.org
#

pkgname=wxgtk
pkgver=2.8.12.1
pkgrel=1
pkgdesc="GTK+ implementation of wxWidgets API for GUI"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'gstreamer0.10-base' 'libgl' 'libxxf86vm')
makedepends=('libgnomeprintui' 'gstreamer0.10-base-plugins' 'gconf' 'mesa')
options=('ccache') # required to build libwx_gtk2u_mmedia
source=(http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2
        configure_1.diff)
#source=(http://downloads.sourceforge.net/wxwindows/wxGTK-${pkgver}.tar.bz2)
md5sums=('8c06c5941477beee213b4f2fa78be620'
         'ff057adcaeb2246e2176588cb437b50d')

build() {
  cd "${srcdir}/wxPython-src-${pkgver}"
#  cd "${srcdir}/wxGTK-${pkgver}"
  patch -Np0 -i ${srcdir}/configure_1.diff
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx --with-gnomeprint --disable-optimize --enable-mediactrl \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys
  make
  make -C locale allmo
  cd contrib/src
  make
}

package() {
  cd "${srcdir}/wxPython-src-${pkgver}"
#  cd "${srcdir}/wxGTK-${pkgver}"
  make DESTDIR="${pkgdir}" install
  cd contrib/src
  make DESTDIR="${pkgdir}" install
  install -D -m644 ../../docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

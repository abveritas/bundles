# Contributor: Paul Bredbury <brebs@sent.com>
# Contributor: Biru Ionut <biru.ionut at gmail.com>
# Contributor: Andrea Fagiani <andfagiani {at} gmail {dot} com>
# Chakra ccr: Matteo Varalta <matteovaralta91 {at} gmail {dot} com>

# Installation order:  freetype2-ubuntu fontconfig-ubuntu libxft-ubuntu cairo-ubuntu
pkgname=cairo-ubuntu
pkgver=1.10.0
_ubver=1.10.0-1ubuntu2
pkgrel=1
pkgdesc="Cairo vector graphics library, with Ubuntu's LCD rendering patches"
arch=(i686 x86_64)
url="https://launchpad.net/ubuntu/natty/+source/cairo"
license=('LGPL' 'MPL')
depends=('libpng>=1.4.0' 'libxrender' 'fontconfig-ubuntu>=2.8.0' 'libxft-ubuntu' 'pixman>=0.16.6' 'xcb-util>=0.3.6')
makedepends=('pkgconfig')
provides=("cairo=${pkgver}" "cairo-tee=${pkgver}")
conflicts=('cairo' 'cairo-cleartype' 'cairo-lcd' 'cairo-tee' 'cairo-xeffects')
options=('!libtool')
source=("http://cairographics.org/releases/cairo-${pkgver}.tar.gz"
        "http://archive.ubuntu.com/ubuntu/pool/main/c/cairo/cairo_${_ubver}.debian.tar.gz"
        cairo-respect-fontconfig.patch)

md5sums=('70a2ece66cf473d976e2db0f75bf199e'
         '38b7e49951f7a6f8545e2a3f78c82869'
         '79f7c141c49f3d65ab308cc706d50914')

build() {
  cd "${srcdir}/cairo-${pkgver}"

  for _f in $(cat "${srcdir}/debian/patches/series"); do
    patch -Np1 -i "${srcdir}/debian/patches/${_f}"
  done

  patch -Np1 -i "${srcdir}/cairo-respect-fontconfig.patch"
  
  ./configure --prefix=/usr --sysconfdir=/etc \
	--localstatedir=/var --disable-static \
        --enable-tee
  make
}

package() {
  cd "${srcdir}/cairo-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # Docs
  # Excluding debian/changelog temporarily
  install -d "${pkgdir}/usr/share/doc/cairo/"
  install -m644 -t "${pkgdir}/usr/share/doc/cairo/" NEWS README
}

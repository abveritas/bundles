# $Id: PKGBUILD 75341 2010-04-01 10:56:43Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Paulius Palevicius <paulius@birzai.com>

pkgname=libgnomecups
pkgver=0.2.3
pkgrel=8
pkgdesc="GNOME cups library"
arch=(i686 x86_64)
license=('LGPL' 'GPL')
url="http://www.gnome.org"
depends=('libcups>=1.3.8-2' 'glib2>=2.16.4' 'krb5' 'gnutls>=2.4.1')
makedepends=('perl-xml-parser')
options=(!libtool)
source=("http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/0.2/${pkgname}-${pkgver}.tar.bz2"
        'libgnomecups_0.2.3-ignore-ipp-not-found.patch'
        'gentoo-libgnomecups-0.2.3-glib.h.patch'
        'gentoo-libgnomecups-0.2.3-g_list_find_custom.patch')
md5sums=('dc4920c15c9f886f73ea74fbff0ae48b'
         '973a1b9d93013ce431400a14b78f5d94'
         'de094a91df09f2136f13e5380b9bfa84'
         '34ffd52e89664aedc2de53589d6d4f40')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # This avoids generating huge 'IPP request failed with status 1030' lines
  patch -Np1 -i "${srcdir}/libgnomecups_0.2.3-ignore-ipp-not-found.patch"
  # Fix compilation with glib2 >= 2.31
  patch -Np1 -i "${srcdir}/gentoo-libgnomecups-0.2.3-glib.h.patch"
  patch -Np1 -i "${srcdir}/gentoo-libgnomecups-0.2.3-g_list_find_custom.patch"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:

# $Id: PKGBUILD 75344 2010-04-01 11:05:03Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libgnomeprint
pkgver=2.18.8
pkgrel=2
pkgdesc="Printing routines for GNOME"
arch=(i686 x86_64)
license=('LGPL' 'GPL')
depends=('pango' 'libart-lgpl' 'libxml2' 'libgnomecups' 'krb5' 'gnutls' 'libgcrypt' 'bzip2')
makedepends=('intltool' 'pkgconfig')
replaces=('libgnomeprint-cups')
conflicts=('libgnomeprint-cups')
url="http://www.gnome.org"
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.18/${pkgname}-${pkgver}.tar.bz2 fix-cups.patch)
sha256sums=('1034ec8651051f84d2424e7a1da61c530422cc20ce5b2d9e107e1e46778d9691'
            'c408a1be8d64c33e806cec11f4fac989b94593b9aeb7415aa45f92cc20ea32fa')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np0 < ${srcdir}/fix-cups.patch || return 1
  ./configure --prefix=/usr --disable-static --disable-gtk-doc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

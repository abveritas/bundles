# Maintainer: Samir Benmendil <samir.benmendil[at]gmail[dot]com>
# Contributor: jbitcm- <chakradeveloper@gmail.com>
 
pkgname=aptana-studio
_pkgname=aptana
pkgver=3.2.0
pkgrel=1
pkgdesc="A professional, open source development tool for the open web."
arch=("i686" "x86_64")
# don't use _arch as '../makepkg' uses it too
[[ "$CARCH" == 'i686' ]]   && _archi=x86
[[ "$CARCH" == 'x86_64' ]] && _archi=x86_64
url="http://www.aptana.com/"
license=("GPLv3")
depends=("gtk2" "gconf" "openjdk6" "xulrunner" "libjpeg-turbo" "libpng12" "net-tools")
makedepends=("unzip")
options=(!strip)
replaces=("aptana")
conflicts=("aptana")
source=("http://download.aptana.com/studio3/standalone/$pkgver/linux/Aptana_Studio_3_Setup_Linux_${_archi}_$pkgver.zip")
[[ "$CARCH" == 'i686' ]]   && md5sums=('9cbbc506288cd4b8d80897df7952c9fb')
[[ "$CARCH" == 'x86_64' ]] && md5sums=('e3b3b6a37490550de8a57b0d701d4138')
 
package() {
    cd "$srcdir/Aptana_Studio_3"
 
    local _instpath="/usr/share/$_pkgname"
 
    # Install Dirs
    find . -type d -exec install -d "{}" "$pkgdir/$_instpath/{}" \; || return 1
 
    # Install Files
    find . -type f -perm 644 -exec install -Dm644 "{}" "$pkgdir/$_instpath/{}" \; || return 1
 
    # Install Executables
    find . -type f -perm 755 -exec install -Dm755 "{}" "$pkgdir/$_instpath/{}" \; || return 1
}

# vim:set ts=2 sw=2 et:


# Maintainer : Anke Boersma <abveritas@chakra-project.org>


pkgbase=('pidgin')
pkgname=('libpurple' 'pidgin' 'finch')
pkgver=2.10.6
pkgrel=1
arch=('i686' 'x86_64')
url="http://pidgin.im/"
license=('GPL')
makedepends=('startup-notification' 'gtkspell' 'libxss' 'nss' 'libsasl' 'python2' 'hicolor-icon-theme' 'libsm' 'libidn' 'gstreamer0.10'
             'farstream' 'avahi' 'tk' 'ca-certificates' 'intltool' 'networkmanager')
options=('!libtool')
source=(http://downloads.sourceforge.net/${pkgbase}/${pkgbase}-${pkgver}.tar.bz2)
sha1sums=('a0532e7ba2acd205d6a5a4e3624156a980fe3d43')

build() {
  cd "$srcdir/${pkgbase}-$pkgver"

    # Use Python 2
    sed -i 's/env python$/&2/' */plugins/*.py \
      libpurple/purple-{remote,notifications-example,url-handler}
    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --disable-schemas-install \
                --disable-meanwhile \
                --disable-perl \
                --disable-gnutls \
                --enable-cyrus-sasl \
                --enable-doxygen \
                --enable-nm \
                --with-python=/usr/bin/python \
                --with-system-ssl-certs=/etc/ssl/certs
    make
}
package_libpurple(){
    pkgdesc="IM library extracted from Pidgin"
    depends=('farstream' 'libsasl' 'dbus-glib' 'silc-toolkit' 'nss' 'cyrus-sasl-plugins')
    optdepends=('dbus-python: for purple-remote and purple-url-handler')
    cd "${srcdir}/${pkgbase}-${pkgver}"

    for dir in libpurple share/sounds share/ca-certs m4macros po
    do
        make -C "${dir}" DESTDIR="${pkgdir}" install
    done
#    rm -rf "$pkgdir/etc" || return 1
}

package_pidgin(){
    pkgdesc="Multi-protocol instant messaging client"
    depends=("libpurple=$pkgver" 'startup-notification' 'gtkspell' 'libxss' 'hicolor-icon-theme' 'gstreamer0.10')

    install=pidgin.install
    cd "${srcdir}/${pkgbase}-${pkgver}"
    #for linking
    make -C libpurple DESTDIR="${pkgdir}" install-libLTLIBRARIES

    make -C pidgin DESTDIR="${pkgdir}" install
    make -C doc DESTDIR="${pkgdir}" install

    #clean up libpurple
    make -C libpurple DESTDIR="${pkgdir}" uninstall-libLTLIBRARIES

    install -D -m 0644 pidgin.desktop "${pkgdir}"/usr/share/applications/pidgin.desktop

    rm -f "${pkgdir}"/usr/share/man/man1/finch.1
}
package_finch(){
    pkgdesc="A ncurses-based messaging client"
    depends=("libpurple=$pkgver" 'python' 'gstreamer0.10')

    cd "${srcdir}/${pkgbase}-${pkgver}"
    #for linking
    make -C libpurple DESTDIR="${pkgdir}" install-libLTLIBRARIES

    make -C finch DESTDIR="${pkgdir}" install
    make -C doc DESTDIR="${pkgdir}" install

    #clean up libpurple
    make -C libpurple DESTDIR="${pkgdir}" uninstall-libLTLIBRARIES
    rm -f "${pkgdir}"/usr/share/man/man1/pidgin.1
}

# vim:set ts=2 sw=2 et:

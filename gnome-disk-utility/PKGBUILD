# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-disk-utility
pkgver=2.32.0
pkgrel=2
pkgdesc="GNOME libraries and applications for dealing with storage devices"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org"
depends=('dbus-glib>=0.86' 'libgnome-keyring>=2.31.92' 'libunique>=1.1.6' 'udisks>=1.0.1' 'libnotify>=0.4.5' 'hicolor-icon-theme' 'avahi')
makedepends=('gnome-doc-utils>=0.20.0' 'intltool')
replaces=('gnome-mount')
conflicts=('gnome-mount')
options=('!libtool' '!emptydirs' '!makeflags')
install=gnome-disk-utility.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.32/${pkgname}-${pkgver}.tar.bz2
        fix-freeze.patch)
sha256sums=('03e461b6bda7f773f8018d25fa3213d3073d4dc83a76e6b39d962652f4de6a98'
            '818b70360978968457d2c50738347c6726ed8d0c007eb57074881a7e33652526')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/fix-freeze.patch"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/gnome-disk-utility \
      --disable-scrollkeeper --disable-nautilus --disable-gtk-doc --disable-static
  make
  make DESTDIR="${pkgdir}" install
}

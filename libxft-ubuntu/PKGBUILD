# Contributor: Paul Bredbury <brebs@sent.com>
# Contributor: Biru Ionut <biru.ionut at gmail.com>
# Maintainer: Andrea Fagiani <andfagiani {at} gmail {dot} com>

# Installation order:  freetype2-ubuntu fontconfig-ubuntu libxft-ubuntu cairo-ubuntu
pkgname=libxft-ubuntu
pkgver=2.2.0
_pkgver=2.1.14-2ubuntu1
pkgrel=1
pkgdesc="FreeType-based font drawing library for X, with Ubuntu's LCD rendering patches"
arch=('i686' 'x86_64')
url="https://launchpad.net/ubuntu/natty/+source/xft"
license=('GPL')
depends=('fontconfig-ubuntu>=2.6.0' 'freetype2-ubuntu' 'libxrender' 'sh')
conflicts=('libxft' 'freetype2-cleartype' 'freetype2-lcd')
provides=("libxft=${pkgver}")
makedepends=('pkgconfig')
options=('!libtool')
source=(http://xorg.freedesktop.org/releases/individual/lib/libXft-$pkgver.tar.bz2
        http://archive.ubuntu.com/ubuntu/pool/main/x/xft/xft_$_pkgver.diff.gz)

md5sums=('cce3c327258116493b753f157e0360c7'
         'b965b9575673ebed2a86992b4096731d')

build() {
  cd "${srcdir}/libXft-${pkgver}"

  patch -Np1 -i "../xft_${_pkgver}.diff"

  for _f in $(cat debian/patches/series) ; do
    [[ "${_f}" = "001_no_export_freetype.diff" ]] && continue
    patch -Np1 -i "debian/patches/${_f}"
  done

  ./configure --prefix=/usr --sysconfdir=/etc --disable-static

  make
}

package() {
  cd "${srcdir}/libXft-${pkgver}"

  make DESTDIR=$pkgdir install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

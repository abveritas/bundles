pkgname=libgnome
_pkgmaj=2.32
_pkgmin=1
pkgver=${_pkgmaj}.${_pkgmin}
pkgrel=2
pkgdesc="Common libraries for GNOME"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('glib2>=2.8.0' 'gnome-vfs>=2.5.3' 'libbonobo>=2.13.0' 'gconf>=1.1.11')
makedepends=('pkgconfig' 'intltool')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
install=libgnome.install
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${_pkgmaj}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('b2c63916866485793b87398266dd7778548c1734923c272a94d84ee011b6f7a4')
 
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
 
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--disable-static \
	--disable-esd \
	--disable-canberra
 
  make
}
 
package() { 
  cd "${srcdir}/${pkgbase}-${pkgver}" 
 
  make -C libgnome DESTDIR="${pkgdir}" install 
  make -C monikers DESTDIR="${pkgdir}" install 
 
  for dir in doc gnome-data po schemas; do 
    make -C $dir GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install 
  done 
 
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas" 
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain libgnome-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas 
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas 
} 

# maintainer: abveritas@chakra-project.org
# contributor: kote <mgonzlezgmz at gmail dot com>


_name=calf
pkgname=$_name-git
pkgver=20120726
pkgrel=1
pkgdesc="LV2/JACK audio plug-ins for musicians"
arch=('i686' 'x86_64')
url="http://calf.sourceforge.net/"
screenshot="http://i.imgur.com/mgYd9.png"
license=('GPL' 'LGPL')
depends=('lv2' 'fluidsynth' 'fftw2' 'lash')
makedepends=('git')
provides=("$_name")
conflicts=("$_name")
options=('!libtool')
install="$pkgname.install"

_gitroot="git://repo.or.cz/$_name.git"
_gitname="$_name"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #


  # DSO link fix (introduced by glib2-2.32.1-1)
  export LIBS=" -lgthread-2.0"

  # fftw2 fix
  sed -i "s/r\?fftw[^_]/d&/g;s/lfftw/ldfftw/g" \
    `grep -rl fftw .`

  ./autogen.sh --prefix="/usr" \
               --enable-static=no \
               --enable-experimental \
               --enable-debug
  make DESTDIR="$pkgdir/"
}

package(){
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
}

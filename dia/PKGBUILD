# Maintainer: Fabien Valthier <hcoohb[ah]gmail[dog]com>

pkgname=dia
pkgver=0.97.2
pkgrel=1
pkgdesc="A GTK+ based diagram creation program"
arch=('i686' 'x86_64')
license=('GPL')
url="http://live.gnome.org/Dia"
depends=('libxslt' 'desktop-file-utils' 'libart-lgpl' 'gtk2')
makedepends=('intltool' 'python2' 'docbook-xsl')
optdepends=('python2')
options=('!libtool' 'docs')
source=("ftp://ftp.gnome.org/pub/gnome/sources/${pkgname}/0.97/${pkgname}-${pkgver}.tar.xz"
	"dia-overflow-fix.patch")
md5sums=('1e1180a513fb567709b09bc19f12105e'
         '8fd9a2ad35b5a6fd8c758d7c73dbfe66')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 <$srcdir/dia-overflow-fix.patch || true

  ./configure --prefix=/usr \
    --with-cairo \
    --with-python \
    --disable-gnome \
    --with-hardbooks
  sed -i 's#SUBDIRS = lib objects plug-ins shapes app bindings samples po sheets data doc tests installer#SUBDIRS = lib objects plug-ins shapes app bindings samples po sheets data tests installer#' Makefile
  make
  cd doc && make html
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  cd doc && make DESTDIR="${pkgdir}" install-html
  ln -sf ../doc/dia/html $pkgdir/usr/share/dia/help
}
